&ACCESS RVP
&REL 1
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF RSI_Ethernet( )
; =============================================
;
; Software License Agreement (BSD License)
;
; Copyright (c) 2014 Norwegian University of Science and Technology
; All rights reserved.
;
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions
; are met:
;
;  * Redistributions of source code must retain the above copyright
;    notice, this list of conditions and the following disclaimer.
;  * Redistributions in binary form must reproduce the above
;    copyright notice, this list of conditions and the following
;    disclaimer in the documentation and/or other materials provided
;    with the distribution.
;  * Neither the name of the Norwegian University of Science and
;    Technology, nor the names of its contributors may be used to
;    endorse or promote products derived from this software without
;    specific prior written permission.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
; "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
; LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
; FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
; COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
; INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
; BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
; CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
; LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
; ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
; POSSIBILITY OF SUCH DAMAGE.
;
; =============================================

; =============================================
;
; ROS INDUSTRIAL: KUKA RSI HW INTERFACE
; Realtime UDP data exchange with
; kuka_rsi_driver
;
; =============================================

; Declaration of KRL variables
DECL INT ret	; Return value for RSI commands

;FOLD INI
  ;FOLD BASISTECH INI
  BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here
  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

; Move to start position
PTP {A1 0, A2 -90, A3 90, A4 0, A5 0, A6 0}

; Create RSI Context
ret = RSI_LOAD("ros_rsi")
IF (ret <> RSIOK) THEN
  HALT
ENDIF

; Activate RSI Context
ret = RSI_ACTIVATE("ros_rsi")
IF (ret <> RSIOK) THEN
  HALT
ENDIF

; Start RSI execution
ret = RSI_PROCESS_ON(#ABSOLUTE)
IF (ret <> RSIOK) THEN
  HALT
ENDIF

; Sensor guided movement
RSI_MOVECORR()

; Turn off RSI
ret = RSI_PROCESS_OFF()
IF (ret <> RSIOK) THEN
  HALT
ENDIF

PTP {A1 0, A2 -90, A3 90, A4 0, A5 0, A6 0}

END
